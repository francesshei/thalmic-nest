neuron aeif_cond:

  state:
    V_m mV = E_L   # Membrane potential
    w pA = 0 pA    # Spike-adaptation current
  end

  equations:
    inline V_bounded mV = min(V_m, V_peak)    # prevent exponential divergence
    kernel g_exc = (1 / (tau_decay_exc - tau_rise_exc)) * (exp(-t / tau_decay_exc) - exp(-t / tau_rise_exc)) * ms
    kernel g_inh = (1 / (tau_decay_inh - tau_rise_inh)) * (exp(-t / tau_decay_inh) - exp(-t / tau_rise_inh)) * ms

    # Add inlines to simplify the equation definition of V_m
    inline exp_arg real = (V_bounded - V_th) / Delta_T
    inline I_spike pA = g_L * Delta_T * exp(exp_arg)
    inline I_syn_exc pA = convolve(g_exc, exc_spikes) * (V_bounded - E_exc)
    inline I_syn_inh pA = convolve(g_inh, inh_spikes) * (V_bounded - E_inh)

    V_m' = (-g_L * (V_bounded - E_L) + I_spike - I_syn_exc - I_syn_inh - w + I_e + I_stim) / C_m
    w' = (a * (V_bounded - E_L) - w) / tau_w
  end

  parameters:
    # membrane parameters
    C_m pF = 281.0 pF       # Membrane Capacitance
    t_ref ms = 0.0 ms       # Refractory period
    V_reset mV = -60.0 mV   # Reset Potential
    g_L nS = 30.0 nS        # Leak Conductance
    E_L mV = -70.6 mV       # Leak reversal Potential (aka resting potential)

    # spike adaptation parameters
    a nS = 4 nS             # Subthreshold adaptation
    b pA = 80.5 pA          # Spike-triggered adaptation
    Delta_T mV = 2.0 mV     # Slope factor
    tau_w ms = 144.0 ms     # Adaptation time constant
    V_th mV = -50.4 mV      # Threshold Potential
    V_peak mV = 0 mV        # Spike detection threshold

    # synaptic parameters
    E_exc mV = 0 mV            # Excitatory reversal Potential
    tau_decay_exc ms = 5.0 ms    # Synaptic Decay Time Constant Excitatory Synapse
    tau_rise_exc ms = 0.4 ms  # Synaptic Rise Time Constant Excitatory Synapse

    E_inh mV = -80.0 mV        # Inhibitory reversal Potential
    tau_decay_inh ms = 20.0 ms    # Synaptic Decay Time Constant for Inhibitory Synapse
    tau_rise_inh ms = 0.4 ms  # Synaptic Rise Time Constant Inhibitory Synapse

    # constant external input current
    I_e pA = 0 pA
  end

  internals:
    # refractory time in steps
    RefractoryCounts integer = steps(t_ref)
    # counts number of tick during the refractory period
    r integer
  end

  input:
    inh_spikes nS <- inhibitory spike
    exc_spikes nS <- excitatory spike
    I_stim pA <- continuous
  end

  output: spike

  update:
    integrate_odes()

    if r > 0: # refractory
      r -= 1 # decrement refractory ticks count
      V_m = V_reset # clamp potential
    elif V_m >= V_peak: # threshold crossing detection
      r = RefractoryCounts + 1
      V_m = V_reset # clamp potential
      w += b
      emit_spike()
    end

  end

end